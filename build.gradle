

group = 'com.vzome'
version = '7.1'

wrapper() {
   gradleVersion = '7.5.1' //version required
}

// These environment variables are automatically available during a Jenkins build
ext.buildNumber = System.getenv( "BUILD_NUMBER" ) ?: '999'
ext.gitCommit = System.getenv( "GIT_COMMIT" ) ?: 'HEAD'
ext.githubRepoName = System.getenv( "GITHUB_REPO_NAME" ) ?: ''

// These environment variables must be configured in the Jenkins job explicitly.
ext.githubClientId = System.getenv( "GITHUB_CLIENT_ID" )
ext.githubClientSecret = System.getenv( "GITHUB_CLIENT_SECRET" )

ext.keychain = System.getenv( "SIGNING_KEYCHAIN" ) ?: 'login.keychain'

ext.appName = 'vZome'
ext.edition = appName

// Specify common settings for all subprojects
subprojects {

  apply plugin: 'java-library'
  apply plugin: 'eclipse'

  compileJava {
	// set these to 11 for Java 11
	// set them to 1.8 for Java 8
    sourceCompatibility = 17
    targetCompatibility = 17
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenCentral()
    
    // this one is for javax.media.jai
    maven { url 'https://repository.springsource.com/maven/bundles/external' }

    // For EGit
    maven { url 'https://repo.eclipse.org/content/groups/releases' }

    maven { url 'https://jogamp.org/deployment/maven' }

    ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR download for vecmath
      url "https://jogamp.org/deployment"
      metadataSources {
        artifact();
      }
      patternLayout {
        artifact "[organization]/[revision]/[artifact].[ext]"
      }
    }
  }

  // see platform/mac/build.gradle, search for "error -5341"
  //    buildDir    = rootProject.buildDir
  group       = rootProject.group
  version     = rootProject.version
  description = rootProject.name + '-' + project.name
}

project(':desktop') {
    dependencies {
        implementation project(':core')
    }
}

project(':server') {
    dependencies {
        implementation project(':desktop')
        implementation project(':core')
    }
}
